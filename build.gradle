//file:noinspection DependencyNotationArgument
plugins {
    id 'java'
    // https://github.com/JetBrains/intellij-platform-gradle-plugin
    // https://plugins.jetbrains.com/docs/intellij/tools-intellij-platform-gradle-plugin.html
    id 'org.jetbrains.intellij.platform' version '2.5.0'
}

group 'com.acme'
version '0.7.14'

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
        marketplace()
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

ext {
    auth0Version = '4.5.0'
    hutoolVersion = '5.8.37'
    jacksonVersion = '2.19.0'
    jsonRepairVersion = '0.3.0'
    fastjson2Version = '2.0.57'
}

dependencies {
    intellijPlatform {
        intellijIdeaCommunity '2025.1'
        bundledPlugin 'org.toml.lang'
        bundledPlugin 'com.intellij.java'
        bundledPlugin 'com.intellij.gradle'
        bundledPlugin 'com.intellij.properties'
        bundledPlugin 'com.intellij.modules.json'
        bundledPlugin 'org.jetbrains.plugins.yaml'
    }
    implementation "com.auth0:java-jwt:${auth0Version}"
    implementation "cn.hutool:hutool-core:${hutoolVersion}"
    implementation "cn.hutool:hutool-http:${hutoolVersion}"
    implementation "io.github.haibiiin:json-repair:${jsonRepairVersion}"
    implementation "com.alibaba.fastjson2:fastjson2:${fastjson2Version}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-toml:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-properties:${jacksonVersion}"
}

runIde {
    // jvm参数优化
    jvmArgs += [
            "-Xms1g",
            "-Xmx4g",
            "-Xshare:off",
            "-Dfile.encoding=UTF-8",
            // 其他优化
            "-XX:+UseParallelGC",
            "-Didea.wsl.disable=true",
            "-XX:ReduceInitialCardSetAllocation=10",
            // 启用模块拓展
            "--add-exports=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED",
            "--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED",
            "--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED",
            "--add-exports=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED",
    ]
    // 设置主题
    systemProperty 'idea.ui.themes', 'Darcula'
    // 设置编码
    systemProperty 'file.encoding', 'UTF-8'
    systemProperty 'idea.encoding.charset', 'UTF-8'
    // 设置插件开发模式
    systemProperty 'idea.is.internal', 'true'
    systemProperty 'idea.auto.update', 'false'
    systemProperty 'idea.dynamic.classpath', 'true'
    systemProperty 'idea.accelerator.manager', 'true'
    systemProperty 'idea.inspections.enabled', 'false'
    systemProperty 'idea.plugins.developer.mode', 'true'
    systemProperty 'idea.plugins.developer.mode.debug', 'true'
}

tasks.register('printVersion') {
    doLast {
        println project.version
    }
}