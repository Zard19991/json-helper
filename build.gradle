//file:noinspection DependencyNotationArgument
plugins {
    id 'java'
    // https://github.com/JetBrains/intellij-platform-gradle-plugin
    // https://plugins.jetbrains.com/docs/intellij/tools-intellij-platform-gradle-plugin.html
    id 'org.jetbrains.intellij.platform' version '2.5.0'
}

group 'com.acme'
version '0.7.10.6'

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

dependencies {
    intellijPlatform {
        intellijIdeaCommunity '2025.1'
        bundledPlugin 'com.intellij.java'
        bundledPlugin 'com.intellij.gradle'
        bundledPlugin 'com.intellij.modules.json'
    }
    implementation 'cn.hutool:hutool-core:5.8.37'
    implementation 'cn.hutool:hutool-http:5.8.37'
    implementation 'com.alibaba.fastjson2:fastjson2:2.0.57'
}

runIde {
    // jvm参数优化
    jvmArgs += [
            "-Xms1g",
            "-Xmx4g",
            "-Xshare:off",
            "-Dfile.encoding=UTF-8",
            // 其他优化
            "-XX:+UseParallelGC",
            "-Didea.wsl.disable=true",
            "-XX:ReduceInitialCardSetAllocation=10",
            // 启用模块拓展
            "--add-exports=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED",
            "--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED",
            "--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED",
            "--add-exports=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED",
    ]
    // 设置主题
    systemProperty 'idea.ui.themes', 'Darcula'
    // 设置编码
    systemProperty 'file.encoding', 'UTF-8'
    systemProperty 'idea.encoding.charset', 'UTF-8'
    // 设置插件开发模式
    systemProperty 'idea.is.internal', 'true'
    systemProperty 'idea.auto.update', 'false'
    systemProperty 'idea.dynamic.classpath', 'true'
    systemProperty 'idea.accelerator.manager', 'true'
    systemProperty 'idea.inspections.enabled', 'false'
    systemProperty 'idea.plugins.developer.mode', 'true'
    systemProperty 'idea.plugins.developer.mode.debug', 'true'
}

tasks.register('printVersion') {
    doLast {
        println project.version
    }
}